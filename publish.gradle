buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath "com.monochromeroad.gradle-plugins:gradle-aws-s3-sync:0.5"
    classpath 'com.github.dcendents:android-maven-plugin:1.2'
  }
}

apply plugin: 'com.github.dcendents.android-maven'
import com.monochromeroad.gradle.plugin.aws.s3.S3Sync


def isReleaseBuild() {
  return !version.contains("SNAPSHOT")
}

def isSnapshotBuild() {
  return !isReleaseBuild()
}

task publishArtifacts(type: S3Sync) {
  dependsOn("assemble","versionCheck","uploadArchives")
  description = "Publishes project artifacts to the Wonder Workshop maven repository"
  accessKey "$System.env.AWS_ACCESS_KEY_ID"
  secretKey "$System.env.AWS_SECRET_ACCESS_KEY"
  keepFiles true
  configFile "jets3t.properties"
  acl com.monochromeroad.gradle.plugin.aws.s3.ACL.PublicRead

  def tgtBucket = isReleaseBuild() ? "wonderworkshop-releases" : "wonderworkshop-snapshots"

  from "$buildDir/repo/"
  into tgtBucket
}


// Sanity check here to make sure nothing on the release
// branch has an invalid version 
def getWorkingBranch() {
  return "git rev-parse --abbrev-ref HEAD".execute().text.trim()
}


task versionCheck(type: DefaultTask) {
  doLast {
    def currentBranch = getWorkingBranch()
    def buildType = isReleaseBuild() ? "RELEASE" : "SNAPSHOT"
    logger.info("Current branch: $currentBranch")
    logger.info("build type: $buildType")
    if( ["release","production"].contains(currentBranch) && isSnapshotBuild() ) {
      throw new GradleException("I think you're trying to perform a SNAPSHOT build but branch '$currentBranch' looks like a release/production branch. Make sure you've updated the 'version' field for this build.")
    } else if (isReleaseBuild()) {
      throw new GradleException("I think you're trying to peform a RELEASE build but branch '$currentBranch' looks like a SNAPSHOT branch. Make sure that 'version' looks something like: 1.0-SNAPSHOT")
    }
  }
}

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: "file://$buildDir/repo")
    }
  }
}
